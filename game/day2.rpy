label day2_intro:
    play music "ost/idle.ogg" volume 0.5 fadein 2.0 loop
    scene office_morning with fade
    narrator_normal "Текущий бюджет: [budget] $"
    narrator_normal "Рейтинг среди жителей: [rating]"
    narrator_normal "{i}Второе утро на посту. Вы только успели налить себе чай, как в кабинет заглядывает помощница.{/i}"

    show a at left
    a "Доброе утро, Георгий! Сегодня день обещает быть непростым."
    a "Я только что получила письмо от Марии — это мама двоих детей из района у реки."
    a "Она очень переживает: после купания у детей появились сыпь и температура. Мария просит срочно принять её."
    a "Я записала её на приём первой. Думаю, стоит выслушать внимательно — в районе уже несколько похожих случаев."
    a "Кстати, вчера вечером в соцсетях появились жалобы от других родителей. Люди волнуются, что вода стала опасной."
    a "После Марии у нас по плану — встреча с инженером водоканала и звонок из Минздрава."
    a "Начинаем?"

    g "Да, зови Марию."

    hide a

    narrator_normal "{i}Через пару минут в кабинет заходит молодая женщина с двумя детьми. Мальчик лет семи и девочка помладше держатся за её руки.{/i}"

    show mother at left
    show child1 at center
    show child2 at right

    m "Здравствуйте, Георгий. Спасибо, что приняли нас так быстро."
    m "Я Мария, живу на улице Прибрежной. Это мои дети — Саша и Лиза."
    "Саша" "Здравствуйте..."
    "Лиза" "Здравствуйте..."

    m "Вчера мы были на пляже, дети купались, а вечером у обоих поднялась температура, появилась сыпь и кашель."
    m "В поликлинике сказали — похоже на аллергию или отравление водой. Но анализы дорогие, а бесплатных нет."
    m "Я очень переживаю. В нашем дворе ещё две семьи жалуются на похожие симптомы у детей."
    m "Помогите, пожалуйста! Мы не знаем, что делать."

label case_mother_questions:
    menu:
        "Спросить, когда начались симптомы":
            m "Вчера вечером, через пару часов после купания. Сначала Лиза пожаловалась на зуд, потом Саша начал кашлять."
            "Лиза" "У меня ещё живот болел..."
            jump case_mother_questions
        "Спросить, где именно купались":
            m "На городском пляже, у старого причала. Там всегда много детей, но в этот раз вода была мутная, с какой-то пеной."
            "Саша" "Я видел, как по реке что-то жёлтое плыло."
            jump case_mother_questions
        "Спросить, что говорят врачи":
            m "Врачи говорят — нужно сдавать анализы, но в поликлинике их нет, а в частной лаборатории это очень дорого."
            m "Они советуют не купаться и не пить воду из-под крана."
            jump case_mother_questions
        "Спросить, есть ли похожие случаи у соседей":
            m "Да, у соседей по подъезду у мальчика тоже сыпь и температура. Его мама уже собирается жаловаться в администрацию."
            jump case_mother_questions
        "Поговорить с детьми":
            jump case_mother_children
        "Перейти к действиям":
            jump case_mother_menu

label case_mother_children:
    menu:
        "Спросить Сашу, как он себя чувствует":
            "Саша" "Горло болит и кожа чешется. Я не хочу больше купаться в реке."
            jump case_mother_children
        "Спросить Лизу, что она помнит о купании":
            "Лиза" "Вода была холодная и пахла странно. А потом у меня живот заболел."
            jump case_mother_children
        "Спросить, пили ли они воду из-под крана":
            m "Да, мы всегда пьем воду из-под крана, фильтра у нас нет."
            "Саша" "Я пил воду после футбола."
            jump case_mother_children
        "Вернуться к вопросам маме":
            jump case_mother_questions
        "Перейти к действиям":
            jump case_mother_menu

label case_mother_menu:
    menu:
        "Выделить деньги на медпомощь (400$)":
            if budget >= 400:
                $ budget -= 400
                $ rating += 2
                m "Спасибо вам огромное! Я сразу отвезу детей к врачу."
                "Оставшийся бюджет: [budget]"
                jump case_mother_action
            else:
                "У вас не хватает средств на медпомощь."
                "Оставшийся бюджет: [budget]"
                jump case_mother_menu
        "Ограничиться заявкой в Минздрав (экономия бюджета)":
            $ rating -= 1
            m "Спасибо, конечно... Но пока Минздрав отреагирует, дети могут заболеть сильнее."
            jump case_mother_action
        "Организовать подвоз чистой воды в район (600$)":
            if budget >= 600:
                $ budget -= 600
                $ rating += 2
                m "Спасибо! Хоть пить теперь не страшно."
                "Оставшийся бюджет: [budget]"
                jump case_mother_action
            else:
                "У вас не хватает средств на подвоз воды."
                "Оставшийся бюджет: [budget]"
                jump case_mother_menu
        "Поручить помощнику заняться этим":
            a "Я займусь этим вопросом и доложу вам к вечеру."
            $ rating += 0
            jump case_mother_action
        "Пожать плечами":
            $ rating -= 2
            m "Я так и знала... Никому мы не нужны."
            jump case_mother_action

label case_mother_action:
    a "Георгий, как поступим дальше?"
    menu:
        "Лично приехать в район и поговорить с жителями":
            scene district_day with fade
            narrator_normal "Вы решаете лично съездить в район у реки, чтобы разобраться на месте."
            narrator_normal "На улице вас встречают встревоженные жители. Несколько мам с детьми показывают бутылки с мутной водой, жалуются на запах и цвет воды из-под крана."
            narrator_normal "Одна из женщин рассказывает, что у её сына после купания началась рвота и сыпь, а у соседей — похожие симптомы."
            narrator_normal "Вы замечаете, что вода в реке действительно мутная, с желтоватой пеной у берега. В воздухе ощущается неприятный химический запах."
            narrator_normal "Жители просят срочно принять меры и опасаются, что ситуация ухудшится."
            show activist at right
            narrator_normal "К вам подходит молодой мужчина с папкой в руках."
            "Алексей Поднебесный" "{cps=40}Здравствуйте, Георгий. Я — Алексей Поднебесный, местный активист. Мы уже собрали десятки жалоб и готовы помочь администрации разобраться.{/cps}"
            "Алексей Поднебесный" "{cps=40}Если вы не против, я могу организовать сбор подписей и контроль за качеством воды вместе с жителями. Это поможет ускорить реакцию властей и привлечь внимание СМИ.{/cps}"
            a "Георгий, что скажете? Может, стоит воспользоваться поддержкой людей?"
            menu:
                "Согласиться на помощь активиста":
                    narrator_normal "Вы благодарите Алексея и договариваетесь о совместных действиях."
                    "Алексей Поднебесный" "{cps=40}Спасибо за доверие! Я сразу начну сбор подписей и оповещу соседей.{/cps}"
                    $ rating += 2
                    narrator_normal "Жители одобрительно кивают, атмосфера становится чуть спокойнее."
                    jump case_day2_after_mother
                "Отказаться, действовать только официально":
                    narrator_normal "Вы вежливо отказываетесь от инициативы, объясняя, что вопрос требует официального расследования."
                    "Алексей Поднебесный" "{cps=40}Понимаю... Но люди всё равно будут требовать ответа.{/cps}"
                    $ rating -= 1
                    narrator_dramatic "Жители выглядят разочарованными."
                    jump case_day2_after_mother
                "Попросить активиста собрать дополнительную информацию":
                    narrator_normal "Вы просите Алексея собрать как можно больше фактов и свидетельств, чтобы передать их в Минздрав и водоканал."
                    "Алексей Поднебесный" "{cps=40}Сделаю! Я соберу все данные и передам вам к вечеру.{/cps}"
                    $ rating += 1
                    narrator_normal "Жители благодарят вас за внимание к их проблемам."
                    jump case_day2_after_mother
        "Поручить помощнику собрать информацию":
            a "Я всё выясню и доложу вам к вечеру."
            jump case_day2_after_mother
        "Оставить всё как есть":
            a "Может, само рассосётся... Но жители будут недовольны."
            $ rating -= 1
            jump case_day2_after_mother

label case_day2_after_mother:
    scene office_day with fade
    narrator_normal "{i}После визита Марии вы возвращаетесь в кабинет. Помощница приносит свежий отчёт из водоканала.{/i}"
    show a at left
    a "Георгий, вот что удалось узнать: сегодня ночью на очистных станциях зафиксировали сбой."
    a "Вода в реке и в трубах действительно мутная, превышение по нитратам и пестицидам."
    a "Водоканал обещает устранить аварию за сутки, но пока пить воду нельзя."
    a "В соцсетях уже обсуждают массовое отравление. Минздрав требует отчёта и предлагает прислать мобильную лабораторию."
    a "Кстати, в мэрии уже раздаётся телефон — журналисты требуют комментариев, а в приёмной очередь из возмущённых жителей."
    a "Что будем делать?"

    menu:
        "Срочно оповестить жителей о запрете пить воду":
            $ rating += 1
            a "Я подготовлю объявление и разошлю по всем каналам."
            narrator_normal "Через полчаса в городских чатах и на сайте администрации появляется предупреждение. Люди благодарят за оперативность, но требуют подробностей."
            jump case_day2_meeting
        "Попросить Минздрав прислать мобильную лабораторию":
            $ rating += 1
            a "Я отправлю заявку. Надеюсь, лаборатория приедет быстро."
            narrator_normal "В соцсетях появляется сообщение: 'Власти реагируют, ждём лабораторию из области.'"
            jump case_day2_meeting
        "Попытаться замять скандал":
            $ rating -= 2
            a "Это рискованно... Если всплывёт, будет хуже."
            narrator_normal "Журналисты всё равно узнают о ситуации, а жители начинают подозревать худшее."
            jump case_day2_meeting

label case_day2_meeting:
    narrator_normal "{i}В коридоре слышны голоса — в кабинет заходит инженер водоканала, за ним — врач из районной поликлиники и представитель МЧС.{/i}"
    show engineer at right
    show doctor at left
    show mchs at center
    "Инженер" "{cps=40}Добрый день, Георгий. Мы готовы доложить обстановку.{/cps}"
    "Доктор" "{cps=40}В больницу уже поступило несколько детей с симптомами отравления. Мы усилили дежурство.{/cps}"
    "Сотрудник МЧС" "{cps=40}МЧС готово организовать подвоз воды и развернуть пункт временной помощи.{/cps}"
    a "Георгий, нужно срочно принять решение. Как действуем?"

    menu:
        "Провести экстренное совещание и распределить задачи":
            narrator_normal "Вы собираете всех в кабинете и быстро распределяете обязанности."
            "Инженер" "{cps=40}Водоканал займётся устранением аварии и контролем качества воды.{/cps}"
            "Доктор" "{cps=40}Медики организуют осмотр всех детей из района и подготовят отчёт.{/cps}"
            "Сотрудник МЧС" "{cps=40}МЧС начнёт подвоз воды и информирование жителей.{/cps}"
            a "Отлично, Георгий! Все службы работают слаженно."
            $ rating += 2
            jump case_day2_ending
        "Поручить всё помощнице и уйти на обед":
            a "Я, конечно, всё сделаю, но люди будут ждать вашего участия."
            narrator_dramatic "Слухи о бездействии главы быстро расходятся по городу."
            $ rating -= 2
            jump case_day2_ending
        "Попросить врача выступить по местному радио":
            "Доктор" "{cps=40}Я готова объяснить ситуацию и дать советы жителям.{/cps}"
            narrator_normal "В эфире врач рассказывает о симптомах и мерах предосторожности. Люди благодарят за честность."
            $ rating += 1
            jump case_day2_ending

label case_day2_ending:
    hide mother
    hide child1
    hide child2
    hide a
    scene office_evening with fade
    a "День окончен. Сегодня вы занимались жалобой Марии и её детей, а также выяснили причину массового отравления."
    narrator_normal"Рейтинг среди жителей: [rating]."
    narrator_normal"Оставшийся бюджет: [budget]"
    play music "ost/night.ogg" volume 0.3 fadein 2.0 loop
    g "Надеюсь, завтра будет легче..."
    narrator_normal"{i}Георгий ложится спать, но тревожные мысли не дают уснуть...{/i}"
    jump day3_intro