label day3_intro:
    scene office_morning with fade
    play music "ost/morning.ogg" volume 0.5 fadein 2.0 loop

    a "Доброе утро, Георгий! Сегодня очень важный день. В городе собрались люди — они переживают из-за качества воды и хотят знать, когда всё наладится."
    a "Министр экологии уже в пути, хочет встретиться с вами. А ещё пришли журналисты — ждут комментариев."
    a "Ваш текущий бюджет: [budget] $"
    g "Похоже, сегодня придётся принимать самые трудные решения..."
    a "Кого примем первым сегодня?"

label day3_choice:
    menu:
        "Журналист":
            jump case_journalist
        "Группа жителей":
            jump case_residents
        "Немного подумать о ситуации":
            jump case3_thoughts

label case3_thoughts:
    $ rating += 1
    if rating >= 5:
        g "Я сделал многое, но впереди ещё трудные решения."
        jump day3_choice
    elif rating >= 0:
        g "Ситуация сложная, но я стараюсь держать всё под контролем."
        jump day3_choice
    else:
        g "Люди недовольны, и я сам не уверен в своих силах."
        jump day3_choice

    menu:
        "Журналист":
            jump case_journalist
        "Группа жителей":
            jump case_residents



label case_journalist:
    show journalist at left
    "В кабинет заходит журналист с микрофоном."
    "Журналист" "Добрый день, Георгий. Горожане волнуются: когда вода станет безопасной? Что делается для решения проблемы?"

label journalist_questions:
    menu:
        "Спросить, что больше всего волнует людей":
            "Журналист" "Люди боятся за здоровье детей и требуют прозрачности."
            jump journalist_questions
        "Спросить, как СМИ освещают ситуацию":
            "Журналист" "Мы стараемся быть объективными, но люди ждут конкретных ответов."
            jump journalist_questions
        "Спросить, есть ли давление со стороны властей":
            "Журналист" "Министерство требует не сеять панику, но мы должны информировать."
            jump journalist_questions
        "Закончить расспросы":
            jump journalist_decision

label journalist_decision:
    menu:
        "Пообещать быстрые перемены":
            $ rating += 1
            "Журналист" "Люди надеются, что вы сдержите слово."
            hide journalist
            jump after_case4
        "Объяснить сложность ситуации":
            "Журналист" "Спасибо за честность, но люди ждут конкретики."
            hide journalist
            jump after_case4
        "Сказать, что ведётся расследование":
            $ rating -= 1
            "Журналист" "Люди устали ждать результатов."
            hide journalist
            jump after_case4
        "Объявить о выделении средств на информирование (500$)":
            if budget >= 500:
                $ budget -= 500
                $ rating += 1
                "Журналист" "Спасибо, теперь люди будут знать больше."
                "Оставшийся бюджет: [budget]"
                hide journalist
                jump after_case4
            else:
                "Журналист" "У вас не хватает средств на информирование."
                jump journalist_decision

label case_residents:
    show residents at left
    "В кабинет заходит группа жителей."
    "Житель" "Мы очень переживаем! Дети болеют, вода пахнет странно, а мы не знаем, когда всё изменится. Что вы собираетесь делать?"

label residents_questions:
    menu:
        "Спросить, когда начались проблемы":
            "Житель" "Уже несколько недель назад, но сейчас стало хуже."
            jump residents_questions
        "Спросить, кто больше всего пострадал":
            "Житель" "В основном дети и пожилые, у них слабый иммунитет."
            jump residents_questions
        "Спросить, обращались ли в больницу":
            "Житель" "Да, врачи говорят — всё из-за воды."
            jump residents_questions
        "Закончить расспросы":
            jump residents_decision

label residents_decision:
    menu:
        "Публично назвать предприятия, которые загрязняют воду":
            $ rating += 2
            "Житель" "Спасибо, что не скрываете правду!"
            hide residents
            jump after_case4
        "Пообещать компенсации пострадавшим (1500$)":
            if budget >= 1500:
                $ budget -= 1500
                $ rating += 1
                "Житель" "Спасибо, хоть кто-то думает о людях."
                "Оставшийся бюджет: [budget]"
                hide residents
                jump after_case4
            else:
                "Житель" "У вас не хватает средств на компенсации."
                "Оставшийся бюджет: [budget]"
                jump residents_decision
        "Попросить потерпеть ещё немного":
            $ rating -= 2
            "Житель" "Мы уже устали ждать..."
            hide residents
            jump after_case4

label after_case4:
    a "Поступило срочное сообщение: министр экологии уже ждёт вас в конференц-зале. В городе растёт напряжение, люди требуют ответа."

    menu:
        "Пойти на встречу с министром":
            jump meeting_minister
        "Поручить помощнику подготовить отчёт":
            jump report_assistant3

label meeting_minister:
    scene conference_hall with fade
    show p at right
    play music "ost/phone.ogg" volume 0.1

    p "Георгий, расскажите, какие меры вы приняли за эти три дня?"

    menu:
        "Рассказать о всех действиях и трудностях":
            $ rating += 1
            p "Спасибо за честность. Это поможет получить поддержку."
        "Сделать вид, что всё под контролем":
            $ rating -= 1
            p "Я вижу, что ситуация сложнее, чем кажется."
        "Попросить экстренную помощь и специалистов (+5000$ в бюджет)":
            $ rating += 2
            $ budget += 5000
            p "Мы выделим дополнительные средства и экспертов."
            "Оставшийся бюджет: [budget]"

    stop music fadeout 1.0
    jump midday3

label report_assistant3:
    a "Я подготовлю отчёт и отправлю министру. Но жители ждут объяснений!"
    jump midday3

label midday3:
    scene city with fade
    play music "ost/ambient_2.ogg" volume 0.5 fadein 2.0 loop

    a "В городе растёт тревога. В соцсетях обсуждают фотографии и ждут вашей реакции."

    menu:
        "Выступить с обращением к жителям":
            $ rating += 1
            g "Дорогие жители! Мы работаем над решением проблемы, организуем уборку и усиливаем контроль."
        "Промолчать":
            $ rating -= 1
            a "Люди волнуются, что власти молчат."

    a "Вечером поступил новый сигнал: в реке снова обнаружены мёртвые птицы. Экологи требуют срочных мер."

label midday3_choice:
    menu:
        "Организовать выездную лабораторию (1000$)":
            if budget >= 1000:
                $ rating += 2
                $ budget -= 1000
                a "Люди видят, что вы действуете."
                "Оставшийся бюджет: [budget]"
            else:
                "У вас не хватает бюджета"
                "Оставшийся бюджет: [budget]"
                jump midday3_choice
        "Поручить разобраться полиции (500$)":
            if budget >= 500:
                $ rating += 1
                $ budget -= 500
                a "Полиция займётся расследованием."
                "Оставшийся бюджет: [budget]"
            else:
                "У вас не хватает бюджета"
                "Оставшийся бюджет: [budget]"
                jump midday3_choice
        "Игнорировать":
            $ rating -= 2
            a "Жители возмущены бездействием."

    jump end_of_day3

label end_of_day3:
    scene office_evening with fade
    play music "ost/night.ogg" volume 0.3 fadein 2.0 loop

    a "Три дня позади. Ваш рейтинг среди жителей: [rating]."
    "Оставшийся бюджет: [budget] $"

    if rating >= 5:
        "Город поддерживает вас! Люди верят, что перемены возможны, и готовы помогать."
        g "Спасибо всем за доверие. Вместе мы справимся!"
    elif rating >= 1:
        "Горожане сомневаются, но надеются на лучшее. Вам предстоит ещё много работы."
        g "Я не подведу вас."
    else:
        "Жители разочарованы. В городе растёт недовольство, и вам грозит отставка."
        g "Я должен был сделать больше..."

    "{i}История только начинается...{/i}"

    return