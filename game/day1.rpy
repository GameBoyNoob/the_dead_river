label day1_intro:
    show intro1
    with dissolve
    play music "ost/Project_8.ogg" volume 1 fadeout 2.0 fadein 2.0 loop

    """На берегах широкой реки раскинулись два города — соперники, веками соревновавшиеся в изобретательности и развитии.

    Их противостояние было источником вдохновения, но однажды всё изменилось.

    После долгих лет борьбы один город уступил, и казалось, что наступил мир.

    Но вместе с этим пришла новая беда: в проигравшем городе началась экологическая катастрофа.

    Заводы, спешно построенные ради экономического рывка, перестали заботиться о реке.

    Вода потемнела, рыба исчезла, а жители всё чаще стали болеть.

    Теперь судьба города — и самой реки — в твоих руках.

    Ты — новый руководитель, которому предстоит сделать трудный выбор:
    спасти людей здесь и сейчас или заложить фундамент для будущего, в котором река вновь станет живой.

    Твои решения определят, каким будет этот город завтра.

    Время действовать."""

    stop music fadeout 3.0

    python:
        name = renpy.input("Как тебя зовут?")
        name = name.strip() or "Георгий"
        budget = 10000  # Бюджет на три дня, например $10,000
        rating = 0

label day1_start:
    hide intro1
    show intro2
    with dissolve
    play music "ost/ambient_2.ogg" volume 0.6 fadeout 2.0 fadein 2.0 loop
    show g say

    g "Мне как-то Георгий будет ближе."

    """И вообще с самого детства я мечтал стать футболистом.
    
    Но родители решили отправить меня на танцы."""

    hide g
    show field
    with dissolve

    "Родители""""Да что тебе этот футбол даст? На танцах куда безопаснее!

    Может, ты девушку себе найдешь наконец."""

    hide field
    with dissolve
    show intro2
    show g talk

    g"А я им говорю."

    hide g
    hide intro2
    with dissolve
    show field
    show g sad

    "Гоша""""Футбол - это моя жизнь.

    Я обожаю бегать по полю, вдыхая ароматы распустившихся цветов.

    Общаться с моими товарищами, надеясь в будущем выйти на большую арену."""

    hide g
    hide field
    with dissolve
    show intro2
    show g talk

    g"""Но мои слова поняли не так как я хотел.

    Стремление купаться в лучах славы восприняли как рвение бесконечно спорить.

    А мою болтовню как склонность к юриспруденции.

    В итоге: Я закончил университет и начал думать куда устроиться.

    Тем временем футбольное поле отдали под завод.

    Весь город начал напоминать рекламу киберпанка."""

    hide g

    "Ведущий Новостей""""Будущее уже наступило!

    Наш город станет центром технологий будущего!

    Уже сейчас мы занимаем первое место по производству микрочипов!

    Мы обещаем что за следующие 8 лет вы станете жить лучше!"""

    show g talk

    g"""А у меня душа болела за мою Родину.

    Чуял я, что это до добра не доведет.

    И кажется, я был прав.

    История повторяется, но теперь ставка куда выше — речь не о гонке технологий, а о выживании целого города."""

    hide g

    show g sad

    """Недавно выборы изменили всё: новым депутатом стал... я.

    Я ещё не знаю всех офисных коридоров наизусть, но уверен — время решительных действий настало."""

    hide g

    "{i}Рабочий день начинается.{/i}"

    a """Георгий, поздравляю с назначением! Я — ваш помощник, и мы оба здесь новенькие.
    Город ждёт перемен… особенно с водой.

    Сегодня к вам приходят жители с жалобами, а министр экологии хочет поговорить."""

    stop music fadeout 1.0

    play music "ost/phone.ogg" volume 0.1
    "{i}Раздаётся звонок.{/i}"
    stop music fadeout 1.0

    play music "ost/ambient_1.ogg" volume 0.6 fadeout 2.0 fadein 2.0 loop

    "Неизвестный" """Здравствуйте, Георгий Скороденок. Знаю, вы только что приняли должность, но времени на раскачку нет — у нас экстренная ситуация.

    Ой, извините, забыл представиться: я из Министерства контроля экологии."""

    p """Вчера вечером наша лаборатория зафиксировала резкое превышение уровня нитратов и пестицидов в воде.

    Основной источник — сбросы от новых фермерских хозяйств, которые используют агрессивные химикаты без контроля.
    Часть из них расположена прямо у реки, и отходы стекают напрямую.

    Вода по анализам признана непригодной не только для питья, но и для бытового использования.

    Уже поступили сообщения о случаях отравления — у людей проблемы с кожей, желудком, есть подозрения на массовое отравление.

    Нам пришлось ввести карантин в двух районах — жителям запрещено набирать воду из-под крана, мы развозим бутилированную, но запасов хватит максимум на три дня.

    Прочтите отчёт экологов: Выезд на место происшествия показал волну негодования среди населения.

    Мы выяснили, что реальный корень проблемы — отсутствие очистительных сооружений и беспорядочное использование пестицидов.

    Вот ваши основные задачи:

    1. Провести встречи с фермерами и выяснить, какие препараты используются.

    2. Проверить отчёты предприятий по фильтрации и утилизации отходов.

    3. Решить, кого штрафовать, кого срочно обязывать установить очистку, а где нужно действовать экстренно — отправлять специальные бригады для очистки воды.

    У вас три дня. Ситуация напряжённая, недовольство жителей растёт. До связи"""

    g "До связи"

    "{i}Звонок заканчивается.{/i}"

    a "Вчера мне писала Анна Петровна по поводу рыбы. С чего начнём, Георгий?"

    stop music fadeout 2.0

label day1_morning:
    play music "ost/morning.ogg" volume 0.5 fadein 2.0 loop
    scene office_morning with fade
    "Текущий бюджет: [budget] $"
    "Бюджет позволяет вам улучшать город, но учтите он не резиновый."
    jump case_babushka

label case_babushka:
    scene office_babushka with dissolve
    show babushka at left

    "Дверь в кабинет с шумом распахивается. Входит Анна Петровна — невысокая, но бойкая бабушка, крепко сжимающая в руках большую чёрную сковородку."
    "Она оглядывается по сторонам, тяжело вздыхает и решительно направляется к столу."
    b "Здравствуй, Георгий!"
    g "Здравствуйте, Анна Петровна. Присаживайтесь, пожалуйста. Что случилось?"
    "Бабушка ставит сковородку на стол так, что та глухо звякает по дереву."
    b "Ой, сынок, слушай! Рыбу жарю — сковородка вся в налёте, а рыба пахнет какой-то дрянью! Всю жизнь ловлю в нашей реке, а теперь и есть страшно. Это сковородка плохая или вода у нас совсем испортилась?"

label case_babushka_questions0:
    menu:
        "Спросить, когда начались проблемы":
            b "Да вот недели две как. Соседка тоже жалуется, у неё руки после мытья чешутся."
            jump case_babushka_questions0
        "Спросить, где ловит рыбу":
            b "Да прямо у моста, как всегда. Там теперь ещё и пена какая-то по утрам."
            jump case_babushka_questions0
        "Спросить, кто ещё жалуется":
            b "Да все жалуются! Только никто ничего не делает."
            jump case_babushka_questions0
        "Поинтерисоваться об ее жизни":
            jump case_babushka_questions1
        "Перейти к действиям":
            jump case_babushka_menu

label case_babushka_questions1:
    menu:
        "Спросить, есть ли у неё семья":
            b "Внучка у меня, Аленка, живёт со мной. Она тоже жалуется — говорит, после душа кожа чешется."
            jump case_babushka_questions1
        "Спросить, как давно она ловит рыбу":
            b "С детства, сынок. Всю жизнь тут живу, никогда такого не было."
            jump case_babushka_questions1
        "Спросить, что думает о причинах":
            b "Да кто ж его знает... Может, завод опять что-то слил, или фермеры свои химикаты льют. А может, трубы старые."
            jump case_babushka_questions1
        "Перейти к действиям":
            jump case_babushka_menu

label case_babushka_questions2:
    menu:
        "Спросить, замечала ли что-то необычное на реке":
            b "Пару раз видела, как ночью у завода свет горит, и вода у берега тёмная. А ещё — дохлая рыба всплывала."
            jump case_babushka_questions2
        "Спросить, как чувствует себя внучка":
            b "Аленка кашляет, говорит, горло першит. Я ей чай завариваю, но всё равно тревожно."
            jump case_babushka_questions2
        "Перейти к действиям":
            jump case_babushka_menu

label case_babushka_menu:
    menu:
        "Отправить анализ рыбы и воды (300$)":
            if budget >= 300:
                $ budget -= 300
                $ rating += 2
                b "Спасибо, сынок! Хоть кто-то слушает."
                "Оставшийся бюджет: [budget]"
                jump case_babushka_action
            else:
                "У вас не хватает средств на анализы."
                "Оставшийся бюджет: [budget]"
                jump case_babushka_menu
        "Вызвать санитарного врача на дом (200$)":
            if budget >= 200:
                $ budget -= 200
                $ rating += 1
                b "Спасибо, милок! Может, хоть врач разберётся."
                "Оставшийся бюджет: [budget]"
                jump case_babushka_action
            else:
                "У вас не хватает средств на врача."
                "Оставшийся бюджет: [budget]"
                jump case_babushka_menu
        "Порекомендовать фильтр для воды (50$)":
            if budget >= 50:
                $ budget -= 50
                $ rating -= 1
                b "Фильтр? А рыбу как фильтровать? Эх, молодёжь..."
                "Оставшийся бюджет: [budget]"
                jump case_babushka_action
            else:
                "У вас не хватает средств даже на фильтр."
                "Оставшийся бюджет: [budget]"
                jump case_babushka_menu
        "Пожать плечами":
            $ rating -= 2
            b "Вот так всегда... Никому мы не нужны."
            jump case_babushka_action

label case_babushka_action:
    a "Георгий, как поступим дальше?"
    menu:
        "Лично поехать на место и осмотреть реку":
            scene river_day with fade
            "Вы приезжаете к реке. На поверхности воды действительно видна пена, а у берега плавает дохлая рыба."
            "Вы замечаете, что у трубы завода вода мутная и пахнет химией."
            a "Похоже, дело серьёзное. Нужно срочно что-то делать."
            jump case_day1_ending
        "Поручить помощнику собрать информацию":
            a "Я всё выясню и доложу вам к вечеру."
            jump case_day1_ending
        "Оставить всё как есть":
            a "Может, само рассосётся... Но жители будут недовольны."
            $ rating -= 1
            jump case_day1_ending

label case_day1_ending:
    hide babushka
    scene office_evening with fade
    a "День окончен. Сегодня вы занимались только жалобой бабушки Анны Петровны."
    "Рейтинг среди жителей: [rating]."
    "Оставшийся бюджет: [budget]"
    play music "ost/night.ogg" volume 0.3 fadein 2.0 loop
    g "Надеюсь, завтра будет легче..."
    "{i}Георгий ложится спать, но тревожные мысли не дают уснуть...{/i}"
    jump day2_intro